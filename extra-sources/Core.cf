position token Id ((char - ["\\\n\t[]():;,.0123456789 "]) (char - ["\\\n\t[]():;,. "])*);

entrypoints Context, Exp, Decl, Seg;

Ctx. Context ::= [Decl] ;

U.        Exp2 ::= "*" ;
Var.      Exp2 ::= Id ;
SegVar.   Exp2 ::= Seg2 "[" [Exp] "]" "." Id ;
App.      Exp1 ::= Exp1 Exp2 ;
Arr.      Exp  ::= Exp1 "->" Exp ;
Abs.      Exp  ::= "[" Id ":" Exp "]" Exp ;
Let.      Exp  ::= "[" Id ":" Exp "=" Exp "]" Exp ;

Dec.      Decl ::= Id ":" Exp ;
Def.      Decl ::= Id ":" Exp "=" Exp ;
DSeg.     Decl ::= Id "=" Seg ;

SegRef.   Seg2 ::= Id ;
SegNest.  Seg2 ::= Seg2 "." Id ;
SegInst.  Seg1 ::= Seg2 "[" [Exp] "]" ;
SegDef.   Seg  ::= "seg" "{" [Decl] "}" ;
_.        Seg  ::= Seg1 ;
_.        Seg1 ::= Seg2 ;

separator Decl ";" ;

separator Exp "," ;

coercions Exp 3;

layout "seg";

layout toplevel;

comment "--";

comment "{-" "-}";

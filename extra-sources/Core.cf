position token Id ((char - ["\\\n\t[]():;,.0123456789 "]) (char - ["\\\n\t[]():;,. "])*);

entrypoints Context, CExp, CDecl;

Ctx. Context ::= [CDecl] ;

U.      CExp2 ::= "*" ;
Var.    CExp2 ::= Id ;
ESeg.   CExp2 ::= "(" Id "[" [CExp] "]" ")" "." Id ;
App.    CExp1 ::= CExp1 CExp2 ;
Arr.    CExp  ::= CExp1 "->" CExp ;
Pi.     CExp  ::= "[" Id ":" CExp "]" CExp ;
Where.  CExp  ::= "[" Id ":" CExp "=" CExp "]" CExp ;

Dec.      CDecl ::= Id ":" CExp ;
Def.      CDecl ::= Id ":" CExp "=" CExp ;
DSeg.     CDecl ::= Id "=" "seg" "{" [CDecl] "}" ;
DSegInst. CDecl ::= Id "=" Id "[" [CExp] "]" ;

separator CDecl ";";

separator CExp "," ;

coercions CExp 3;

coercions CDecl 1;

layout "seg";

layout toplevel;

comment "--";

comment "{-" "-}";
